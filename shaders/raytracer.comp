#version 460 core

layout (local_size_x = 32, local_size_y = 32, local_size_z = 1) in;

layout (rgba32f, binding = 0) uniform image2D imgOutput;
layout (location = 0) uniform float time;

layout (location = 1) uniform vec3 camera_position; // x, y, z
layout (location = 2) uniform vec3 camera_orientation;  // pitch, yaw and fov
layout (location = 3) uniform mat4 camera_view;
layout (location = 4) uniform mat4 camera_projection;
layout (location = 5) uniform float near_plane;
layout (location = 6) uniform float far_plane;

#define N_SPHERES 9

struct sphere_t {
  vec3 position;
  float radius;
  vec3 color;
};


void main() {
  vec4 value = vec4(0.0, 0.0, 0.0, 1.0);
  ivec2 pixel_position = ivec2(gl_GlobalInvocationID.xy);
  ivec2 texture_size = imageSize(imgOutput);

  sphere_t spheres[N_SPHERES] = {
    {vec3(  0.0 ,-0.5 , -9.0) , 1.25 , vec3(1.0 , 0.0 , 0.0)} ,
    {vec3(  0.0 , 1.5 , -9.0) , 1.25 , vec3(0.0 , 0.0 , 1.0)} ,
    {vec3(  2.0 , 0.5 ,-11.0) , 2.5  , vec3(0.0 , 1.0 , 0.0)} ,
    {vec3(  0.0 , 0.0 ,-11.0) , 2.5  , vec3(0.3 , 0.8 , 0.2)} ,
    {vec3(  0.0 , 0.0 , 10.0) , 2.5  , vec3(0.0 , 0.1 , 0.8)} ,
    {vec3(  0.0 , 10.0,  0.0) , 2.5  , vec3(0.0 , 0.1 , 0.8)} ,
    {vec3(  0.0 ,-10.0,  0.0) , 2.5  , vec3(0.0 , 0.1 , 0.8)} ,
    {vec3( 10.0,  0.0 ,  0.0) , 2.5  , vec3(0.0 , 0.1 , 0.8)} ,
    {vec3(-10.0,  0.0 ,  0.0) , 2.5  , vec3(0.0 , 0.1 , 0.8)} ,
  };

  float aspect_ratio = float(texture_size.x) / float(texture_size.y);
  float max_y = 5.0;
  float max_x = max_y * aspect_ratio;

  float x = (float(pixel_position.x * 2 - texture_size.x) / texture_size.x);
  float y = (float(pixel_position.y * 2 - texture_size.y) / texture_size.y);
  vec3 ray_o = vec3(x * max_x, y * max_y, 0.0);
  vec3 ray_d = vec3(0.0, 0.0, -1.0); // ortho

  float hit_distance = far_plane;
  int sphere_hit_id = -1;

  for (int i = 0; i < N_SPHERES; i++) {
    vec4 pos = vec4(spheres[i].position, 1.0);
    pos = camera_projection * camera_view * pos;
    vec3 omc = ray_o - pos.xyz;
    float b = dot(ray_d, omc);
    float c = dot(omc, omc) - spheres[i].radius * spheres[i].radius;
    float bsqmc = b * b - c;

    if (bsqmc >= 0.0) {
      float t = -b - sqrt(bsqmc);
      if (t > near_plane && t < hit_distance) {
        hit_distance = t;
        sphere_hit_id = i;
      }
    }
  }

  if (sphere_hit_id >= 0) {
    value.xyz = spheres[sphere_hit_id].color;
  }

  imageStore(imgOutput, pixel_position, value);
}
